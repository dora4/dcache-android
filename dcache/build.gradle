apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'

group = 'com.github.dora4'

android {
    compileSdkVersion this.compile_sdk
    defaultConfig {
        minSdkVersion this.min_sdk
        targetSdkVersion this.target_sdk
        versionCode this.release_version_code
        versionName this.release_version_name
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingAar(project, android)
    compileOptions {
        kotlinOptions.freeCompilerArgs += ['-module-name', "dcache"]
    }
}

void packagingAar(project, android) {
    android.libraryVariants.all { variant ->
        if (variant.buildType.name == "debug") {
            // 修改aar名称
            variant.outputs.all { output ->
                outputFileName = "${project.name}-v${android.defaultConfig.versionName}@${variant.buildType.name}.aar"
            }
            // 复制aar到指定目录
            variant.assemble.doLast {
                variant.outputs.all { output ->
                    // 输出目录在：项目目录/build/outputs/aar/
                    def outputPath = "${project.rootDir.absolutePath}/build/outputs/aar"
                    copy {
                        from output.outputFile
                        into outputPath
                    }
                }
            }
        } else if (variant.buildType.name == "release") {
            // 修改aar名称
            variant.outputs.all { output ->
                outputFileName = "${project.name}-v${android.defaultConfig.versionName}.aar"
            }
            // 复制aar到指定目录
            variant.assemble.doLast {
                variant.outputs.all { output ->
                    // 输出目录在：项目目录/build/outputs/aar/
                    def outputPath = "${project.rootDir.absolutePath}/build/outputs/aar"
                    copy {
                        from output.outputFile
                        into outputPath
                    }
                }
            }
        }
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.8.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.squareup.retrofit2:retrofit:2.7.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.2'
}
